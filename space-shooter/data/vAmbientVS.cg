/*--------------------------------------------------------------------------------------
 Ethanon Engine (C) Copyright 2008-2013 Andre Santee
 http://ethanonengine.com/

	Permission is hereby granted, free of charge, to any person obtaining a copy of this
	software and associated documentation files (the "Software"), to deal in the
	Software without restriction, including without limitation the rights to use, copy,
	modify, merge, publish, distribute, sublicense, and/or sell copies of the Software,
	and to permit persons to whom the Software is furnished to do so, subject to the
	following conditions:

	The above copyright notice and this permission notice shall be included in all
	copies or substantial portions of the Software.

	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
	INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
	PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
	HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
	CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE
	OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
--------------------------------------------------------------------------------------*/

// the following global uniform parameters are set automaticaly
// by the GameSpaceLib runtime

// sprite and screen properties:
uniform float4x4 viewMatrix;     // orthogonal matrix for the screen space
uniform float4x4 rotationMatrix; // sprite rotation matrix
uniform float2 screenSize;       // current screen size
uniform float2 size;             // sprite width and height
uniform float2 entityPos;        // sprite position
uniform float2 center;           // sprite origin (in pixels)
uniform float2 bitmapSize;       // sprite size
uniform float2 rectPos;          // texture rect cut position (in pixels)
uniform float2 rectSize;         // texture rect cut size (in pixels)
uniform float2 scroll;           // texture scroll parameter
uniform float2 multiply;         // texture multiply
uniform float4 color0;           // left-top vertex color
uniform float4 color1;           // right-top vertex color
uniform float4 color2;           // left-bottom vertex color
uniform float4 color3;           // right-bottom vertex color
uniform float2 flipAdd;          // this is added to the vertex position to flip it
uniform float2 flipMul;          // this is multiplied by the vertex position do flip it
uniform float2 cameraPos;
uniform float depth;

uniform float3 parallaxOrigin_verticalIntensity;
uniform float parallaxIntensity;
uniform float4 entityPos3D;



float computeVerticalOffsetInPixels(const float posY, const float rectSizeY)
{
	return ((1 - posY) * rectSizeY);
}

float2 computeParallaxOffset(const float2 vertPos, float3 inPosition)
{
	const float vOffset = computeVerticalOffsetInPixels(inPosition.y, rectSize.y);
	const float2 parallaxOrigin = parallaxOrigin_verticalIntensity.xy;
	const float verticalIntensity = parallaxOrigin_verticalIntensity.z;
	return ((vertPos - parallaxOrigin) / screenSize.x) * (entityPos3D.z + (vOffset * verticalIntensity)) * parallaxIntensity;
}

// returns the sprite vertex position according to it's
// origin in screen space and rotation angle
float4 transformSprite(float3 position)
{
	float4 newPos = float4(position, 1) * float4(flipMul,1,1) + float4(flipAdd,0,0);

	// scales and position the sprite center
	newPos = newPos * float4(size,1,1) - float4(center, 0, 0);

	// rotates the sprite
	newPos = mul(rotationMatrix, newPos);

	// position the sprite according to screen space
	newPos += float4(entityPos,0,0) - float4(cameraPos,0,0);
	newPos += float4(computeParallaxOffset(newPos.xy, position),0,0) - float4(screenSize / 2,0,0);

	// inverts the y coordinate
	newPos *= float4(1,-1,1,1);
	return mul(viewMatrix, newPos);
}

// returns the texture coordinate according to the rect
float2 transformCoord(float2 texCoord)
{
	// adjusts the texture coordinate according to the cut-rect
	float2 newCoord = texCoord * (rectSize / bitmapSize);
	newCoord += (rectPos / bitmapSize);

	// scrolls and multiplies the texture
	return (newCoord);
}

void main(  float3 inPosition : POSITION,
			float2 texCoord : TEXCOORD0,
			out float4 oPosition : POSITION,
			out float4 oColor    : COLOR0,
			out float2 oTexCoord0 : TEXCOORD0,
			out float2 oTexCoord1 : TEXCOORD1,
			uniform float spaceLength)
{
	float4 outPos = transformSprite(inPosition);
	outPos.z = (1 - depth) - ((computeVerticalOffsetInPixels(inPosition.y, rectSize.y)) / spaceLength);
	oPosition = outPos;
	float2 coord = transformCoord(texCoord);
	oTexCoord0 = coord;
	oTexCoord1 = texCoord;
	oColor = color0;
}
